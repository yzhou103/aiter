// SPDX-License-Identifier: MIT
// Copyright (C) 2026, Advanced Micro Devices, Inc. All rights reserved.

#pragma once

#include "hk_mla_utils.cuh"

using namespace hk_mla;

template <typename T>
class QManager8bitsV1
{
    private:
    using q_t = typename T::q_t;

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return 0; // Not used
    }

    template <uint32_t GPR_NOPE_START, uint32_t GPR_ROPE_START>
    __device__ __forceinline__ static void load_q_to_gpr(const typename T::gl_q& q_buffer,
                                                         const int32_t warp_idx,
                                                         const int32_t q_start,
                                                         const uintptr_t p_lds)
    {
        using q_nope_ranges = hkdart::split_many_t<
            hkdart::type_list<hkdart::range<GPR_NOPE_START, GPR_NOPE_START + 32 - 1>>,
            2>; // 32 vgprs
        using q_rope_ranges = hkdart::split_many_t<
            hkdart::type_list<hkdart::range<GPR_ROPE_START, GPR_ROPE_START + 4 - 1>>,
            2>; // 4 vgprs

        static hk::art<q_t, T::kTileM, T::kQkNopeHeadDim, hk::row_l, hk::rt_16x32_s, q_nope_ranges>
            q_nope;
        static hk::art<q_t, T::kTileM, T::kQkRopeHeadDim, hk::row_l, hk::rt_16x32_s, q_rope_ranges>
            q_rope;

        hk::load<2, 0>(q_nope, q_buffer, {q_start, 0, 0, 0}, {0, warp_idx, 0, 0});
        hk::load<2, T::kQkNopeHeadDim>(q_rope, q_buffer, {q_start, 0, 0, 0}, {0, warp_idx, 0, 0});
    }
};

// Lanes load Q from VRAM by row so as to fulfill cache line. Then, lanes exchange data via
// ds_bpermute_b32.
template <typename T>
class QManager8bitsV2
{
    private:
    using q_t = typename T::q_t;

    uint32_t m_src_lane_0;
    uint32_t m_src_lane_1;
    uint64_t m_use_src1_s;

    template <uint32_t GPR_START>
    __device__ __forceinline__ void shuffle_data(const v4ui& data)
    {
        uint32_t src_lane_0_reg_0;
        uint32_t src_lane_0_reg_1;
        uint32_t src_lane_0_reg_2;
        uint32_t src_lane_0_reg_3;
        uint32_t src_lane_1_reg_0;
        uint32_t src_lane_1_reg_1;
        uint32_t src_lane_1_reg_2;
        uint32_t src_lane_1_reg_3;

        asm volatile("ds_bpermute_b32 %0, %4, %5\n\t"
                     "ds_bpermute_b32 %2, %4, %7\n\t"
                     "ds_bpermute_b32 %1, %4, %6\n\t"
                     "ds_bpermute_b32 %3, %4, %8"
                     : "=v"(src_lane_0_reg_0),
                       "=v"(src_lane_0_reg_1),
                       "=v"(src_lane_0_reg_2),
                       "=v"(src_lane_0_reg_3)
                     : "v"(m_src_lane_0), "v"(data[0]), "v"(data[1]), "v"(data[2]), "v"(data[3]));

        // Workaround for quality issue under 8 waves mode. The results of wave 4-7 may be
        // incorrect if there are more than 4 ds_bpermute_b32 launched in short term.
        if constexpr(T::kNumWarps > 4)
        {
            __builtin_amdgcn_s_barrier();
        }

        asm volatile("ds_bpermute_b32 %0, %4, %5\n\t"
                     "ds_bpermute_b32 %2, %4, %7\n\t"
                     "ds_bpermute_b32 %1, %4, %6\n\t"
                     "ds_bpermute_b32 %3, %4, %8"
                     : "=v"(src_lane_1_reg_0),
                       "=v"(src_lane_1_reg_1),
                       "=v"(src_lane_1_reg_2),
                       "=v"(src_lane_1_reg_3)
                     : "v"(m_src_lane_1), "v"(data[0]), "v"(data[1]), "v"(data[2]), "v"(data[3]));

        asm volatile("s_waitcnt lgkmcnt(6)\n\t"
                     "v_cndmask_b32 v[%0], %4, %8, %12\n\t"
                     "s_waitcnt lgkmcnt(4)\n\t"
                     "v_cndmask_b32 v[%1], %5, %9, %12\n\t"
                     "s_waitcnt lgkmcnt(2)\n\t"
                     "v_cndmask_b32 v[%2], %6, %10, %12\n\t"
                     "s_waitcnt lgkmcnt(0)\n\t"
                     "v_cndmask_b32 v[%3], %7, %11, %12"
                     :
                     : "i"(GPR_START),
                       "i"(GPR_START + 1),
                       "i"(GPR_START + 2),
                       "i"(GPR_START + 3),
                       "v"(src_lane_0_reg_0),
                       "v"(src_lane_0_reg_1),
                       "v"(src_lane_1_reg_0),
                       "v"(src_lane_1_reg_1),
                       "v"(src_lane_0_reg_2),
                       "v"(src_lane_0_reg_3),
                       "v"(src_lane_1_reg_2),
                       "v"(src_lane_1_reg_3),
                       "s"(m_use_src1_s));
    }

    public:
    __device__ QManager8bitsV2()
    {
        const uint32_t lane_idx = opus::lane_id();
        m_src_lane_0            = (lane_idx % 16) * 4 + (lane_idx / 32);
        m_src_lane_1            = m_src_lane_0 + 2;
        m_src_lane_0 *= 4; // the address passed in ds_bpermute_b32 is tid * 4
        m_src_lane_1 *= 4;

        const uint32_t use_src1_v = (lane_idx / 16) % 2;
        asm volatile("v_cmp_ne_u32 %0, %1, %2" : "=s"(m_use_src1_s) : "v"(use_src1_v), "v"(0));
    }

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return 0; // Not used
    }

    template <uint32_t GPR_NOPE_START, uint32_t GPR_ROPE_START>
    __device__ __forceinline__ void load_q_to_gpr(const typename T::gl_q& q_buffer,
                                                  const int32_t warp_idx,
                                                  const int32_t q_start,
                                                  const uintptr_t p_lds)
    {
        // Each warp loads 16x64 each time. Each lane handles 1x16 elements.
        // Since dtype should be fp8, a buffer_load_dwordx4 is used to load all 1x16 elements.
        constexpr uint32_t kNumRowsPerWarp = 16;
        constexpr uint32_t kNumColsPerWarp = 64;
        constexpr uint32_t kNumElemPerWarp = kNumRowsPerWarp * kNumColsPerWarp;       // 16*64=1024
        constexpr uint32_t kNumElemPerLane = kNumElemPerWarp / opus::get_warp_size(); // 1024/64=16
        constexpr uint32_t kNumLanesPerRow = kNumColsPerWarp / kNumElemPerLane;       // 64/16=4

        const uint32_t lane_idx = opus::lane_id();

        uint64_t as_u64 =
            static_cast<uint64_t>(reinterpret_cast<uintptr_t>(&q_buffer[{q_start, 0, 0, 0}]));
        const hk::buffer_resource br = hk::make_buffer_resource(as_u64, 0xffffffff, 0x00020000);

        const uint32_t s_offset = warp_idx * kNumRowsPerWarp * T::kQkHeadDim * sizeof(q_t);
        const uint32_t row      = lane_idx / kNumLanesPerRow;
        const uint32_t col      = (lane_idx % kNumLanesPerRow) * kNumElemPerLane;
        const uint32_t v_offset = (row * T::kQkHeadDim + col) * sizeof(q_t);

        v4ui data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 0 * kNumColsPerWarp);
        v4ui data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 1 * kNumColsPerWarp);
        asm volatile("s_waitcnt vmcnt(1)");
        v4ui data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 2 * kNumColsPerWarp);
        __builtin_amdgcn_s_setprio(3);
        shuffle_data<GPR_NOPE_START + 0>(data_0);
        asm volatile("s_waitcnt vmcnt(1)");
        data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 3 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 4>(data_1);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(2);
        data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 4 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 8>(data_2);
        asm volatile("s_waitcnt vmcnt(1)");
        data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 5 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 12>(data_0);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(1);
        data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 6 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 16>(data_1);
        asm volatile("s_waitcnt vmcnt(1)");
        data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 7 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 20>(data_2);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(0);
        data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 8 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 24>(data_0);
        asm volatile("s_waitcnt vmcnt(1)");
        shuffle_data<GPR_NOPE_START + 28>(data_1);
        asm volatile("s_waitcnt vmcnt(0)");
        shuffle_data<GPR_ROPE_START>(data_2);
    }
};

// Lanes load Q from VRAM by row so as to fulfill cache line. Then, lanes exchange data via LDS.
template <typename T>
class QManager8bitsV3
{
    private:
    using q_t = typename T::q_t;

    // Stores 16x64 elements per warp in LDS.
    // Pad 2DW per 2 rows.
    static constexpr uint32_t kNumElemPerRow           = 64;
    static constexpr uint32_t kNumElemPerCol           = 16;
    static constexpr uint32_t kNumPaddingBytesPer2Rows = 2 * sizeof(uint32_t); // 2*4=8
    static constexpr uint32_t kNumBytesPer2Rows =
        kNumElemPerRow * 2 * sizeof(q_t) + kNumPaddingBytesPer2Rows; // 64*2*1+8=128+8=136

    // All come from mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaRows = 16;
    static constexpr uint32_t kMfmaCols = 32;
    static constexpr uint32_t kMfmaElemPerLane =
        kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*32/64=8

    template <uint32_t GPR_START>
    __device__ __forceinline__ void shuffle_data(const v4ui& data, const uintptr_t p_lds)
    {
        constexpr uint32_t kNumLanePerRow = opus::get_warp_size() / kNumElemPerCol; // 64/16=4

        const uint32_t lane_idx = opus::lane_id();

        auto get_v_offset = [&](const uint32_t row, const uint32_t col) -> uint32_t {
            return (row / 2) * kNumBytesPer2Rows + ((row % 2) * kNumElemPerRow + col) * sizeof(q_t);
        };

        const uint32_t row_st = lane_idx / kNumLanePerRow;
        const uint32_t col_st = (lane_idx % kNumLanePerRow) * (kNumElemPerRow / kNumLanePerRow);
        const uint32_t v_offset_st = get_v_offset(row_st, col_st);

        const uint32_t row_ld      = lane_idx % kMfmaRows;
        const uint32_t col_ld      = (lane_idx / kMfmaRows) * kMfmaElemPerLane;
        const uint32_t v_offset_ld = get_v_offset(row_ld, col_ld);

        v4ui data_v = {data.x, data.y, data.z, data.w};

        asm volatile("s_waitcnt lgkmcnt(0)");
        hkm::ds_write_b128(data_v, p_lds + v_offset_st, 0);
        asm volatile("s_waitcnt lgkmcnt(0)");
        hkm::ds_read_b64<GPR_START + 0>(p_lds + v_offset_ld, 0);
        hkm::ds_read_b64<GPR_START + 2>(p_lds + v_offset_ld, kMfmaCols * sizeof(q_t));
    }

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_per_warp_in_byte()
    {
        // 16/2 * 136 = 1088
        static_assert(kNumElemPerCol % 2 == 0, "kNumElemPerCol must be even!");
        return kNumElemPerCol / 2 * kNumBytesPer2Rows;
    }

    public:
    __device__ QManager8bitsV3() {}

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    { return T::kNumWarps * get_lds_size_per_warp_in_byte(); }

    template <uint32_t GPR_NOPE_START, uint32_t GPR_ROPE_START>
    __device__ __forceinline__ void load_q_to_gpr(const typename T::gl_q& q_buffer,
                                                  const int32_t warp_idx,
                                                  const int32_t q_start,
                                                  const uintptr_t p_lds)
    {
        // Each warp loads 16x64 each time. Each lane handles 1x16 elements.
        // Since dtype should be fp8, a buffer_load_dwordx4 is used to load all 1x16 elements.
        constexpr uint32_t kNumRowsPerWarp = 16;
        constexpr uint32_t kNumColsPerWarp = 64;
        constexpr uint32_t kNumElemPerWarp = kNumRowsPerWarp * kNumColsPerWarp;       // 16*64=1024
        constexpr uint32_t kNumElemPerLane = kNumElemPerWarp / opus::get_warp_size(); // 1024/64=16
        constexpr uint32_t kNumLanesPerRow = kNumColsPerWarp / kNumElemPerLane;       // 64/16=4

        const uint32_t lane_idx = opus::lane_id();

        const uintptr_t p_lds_warp = p_lds + warp_idx * get_lds_size_per_warp_in_byte();

        uint64_t as_u64 =
            static_cast<uint64_t>(reinterpret_cast<uintptr_t>(&q_buffer[{q_start, 0, 0, 0}]));
        const hk::buffer_resource br = hk::make_buffer_resource(as_u64, 0xffffffff, 0x00020000);

        const uint32_t s_offset = warp_idx * kNumRowsPerWarp * T::kQkHeadDim * sizeof(q_t);
        const uint32_t row      = lane_idx / kNumLanesPerRow;
        const uint32_t col      = (lane_idx % kNumLanesPerRow) * kNumElemPerLane;
        const uint32_t v_offset = (row * T::kQkHeadDim + col) * sizeof(q_t);

        v4ui data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 0 * kNumColsPerWarp);
        v4ui data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 1 * kNumColsPerWarp);
        asm volatile("s_waitcnt vmcnt(1)");
        v4ui data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 2 * kNumColsPerWarp);
        __builtin_amdgcn_s_setprio(3);
        shuffle_data<GPR_NOPE_START + 0>(data_0, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 3 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 4>(data_1, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(2);
        data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 4 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 8>(data_2, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 5 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 12>(data_0, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(1);
        data_0 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 6 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 16>(data_1, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        data_1 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 7 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 20>(data_2, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        __builtin_amdgcn_s_setprio(0);
        data_2 = hkm::buffer_load_dwordx4(br, v_offset, s_offset, 8 * kNumColsPerWarp);
        shuffle_data<GPR_NOPE_START + 24>(data_0, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(1)");
        shuffle_data<GPR_NOPE_START + 28>(data_1, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        shuffle_data<GPR_ROPE_START>(data_2, p_lds_warp);
    }
};

// Compared with V3, V4 uses LDS async load.
template <typename T>
class QManager8bitsV4
{
    protected:
    using q_t = typename T::q_t;

    // Stores 16x64 elements per warp in LDS.
    // Pad 4DW per 4 rows.
    static constexpr uint32_t kNumElemPerRow           = 64;
    static constexpr uint32_t kNumElemPerCol           = 16;
    static constexpr uint32_t kNumPaddingBytesPer4Rows = 4 * sizeof(uint32_t); // 4*4=16
    static constexpr uint32_t kNumBytesPer4Rows =
        kNumElemPerRow * 4 * sizeof(q_t) + kNumPaddingBytesPer4Rows; // 64*4*1+16=256+16=272

    // All come from mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaRows = 16;
    static constexpr uint32_t kMfmaCols = 32;
    static constexpr uint32_t kMfmaElemPerLane =
        kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*32/64=8

    // The input ptrs are expected to be the start address of the warp.
    // After loading, the data layout in LDS is:
    // (00, 00 - 07) [Lane00 - Lane01], (00, 32 - 39) [Lane02 - Lane03]
    // (00, 08 - 15) [Lane04 - Lane05], (00, 40 - 47) [Lane06 - Lane07]
    // (00, 16 - 23) [Lane08 - Lane09], (00, 48 - 55) [Lane10 - Lane11]
    // (00, 24 - 31) [Lane12 - Lane13], (00, 56 - 63) [Lane14 - Lane15]
    // (01, 00 - 07) [Lane16 - Lane17], (01, 32 - 39) [Lane18 - Lane19]
    // (01, 08 - 15) [Lane20 - Lane21], (01, 40 - 47) [Lane22 - Lane23]
    // (01, 16 - 23) [Lane24 - Lane25], (01, 48 - 55) [Lane26 - Lane27]
    // (01, 24 - 31) [Lane28 - Lane29], (01, 56 - 63) [Lane30 - Lane31]
    // (08, 00 - 07) [Lane00 - Lane01], (08, 32 - 39) [Lane02 - Lane03]
    // (08, 08 - 15) [Lane04 - Lane05], (08, 40 - 47) [Lane06 - Lane07]
    // (08, 16 - 23) [Lane08 - Lane09], (08, 48 - 55) [Lane10 - Lane11]
    // (08, 24 - 31) [Lane12 - Lane13], (08, 56 - 63) [Lane14 - Lane15]
    // (09, 00 - 07) [Lane16 - Lane17], (09, 32 - 39) [Lane18 - Lane19]
    // (09, 08 - 15) [Lane20 - Lane21], (09, 40 - 47) [Lane22 - Lane23]
    // (09, 16 - 23) [Lane24 - Lane25], (09, 48 - 55) [Lane26 - Lane27]
    // (09, 24 - 31) [Lane28 - Lane29], (09, 56 - 63) [Lane30 - Lane31]
    // 4DW padding
    // (02, 00 - 07) [Lane00 - Lane01], (02, 32 - 39) [Lane02 - Lane03]
    // ...
    template <uint32_t kColOffset>
    __device__ __forceinline__ void vram_2_lds(const q_t* p_q_buffer, const uintptr_t p_lds)
    {
        constexpr uint32_t kOffsetInBytes = kColOffset * sizeof(q_t);

        const uint32_t lane_idx = opus::lane_id();

        const uint32_t row_tmp = lane_idx / 16;
        const uint32_t row     = (row_tmp / 2) * (kNumElemPerCol / 2) + (row_tmp % 2) * 1;
        const uint32_t col_tmp = lane_idx % 16;
        const uint32_t col =
            (col_tmp / 2) % 2 * (kNumElemPerRow / 2) + (col_tmp / 4) * 8 + (col_tmp % 2) * 4;
        constexpr uint32_t voffset_inc = 2 * T::kQkHeadDim * sizeof(q_t) - kNumBytesPer4Rows;

        const hk::i32x4 srsrc = hk::make_srsrc(p_q_buffer, 0xffffffff);

        uint32_t voffset = (row * T::kQkHeadDim + col) * sizeof(q_t);
        hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                            (hk::as3_uint32_ptr)(p_lds - kOffsetInBytes),
                                            4,
                                            voffset,
                                            0,
                                            kOffsetInBytes + kNumBytesPer4Rows * 0,
                                            0);
        voffset += voffset_inc;
        hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                            (hk::as3_uint32_ptr)(p_lds - kOffsetInBytes),
                                            4,
                                            voffset,
                                            0,
                                            kOffsetInBytes + kNumBytesPer4Rows * 1,
                                            0);
        voffset += voffset_inc;
        hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                            (hk::as3_uint32_ptr)(p_lds - kOffsetInBytes),
                                            4,
                                            voffset,
                                            0,
                                            kOffsetInBytes + kNumBytesPer4Rows * 2,
                                            0);
        voffset += voffset_inc;
        hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                            (hk::as3_uint32_ptr)(p_lds - kOffsetInBytes),
                                            4,
                                            voffset,
                                            0,
                                            kOffsetInBytes + kNumBytesPer4Rows * 3,
                                            0);
    }

    template <uint32_t GPR_START>
    __device__ __forceinline__ void lds_2_gpr(const uintptr_t p_lds)
    {
        const uint32_t lane_idx = opus::lane_id();

        const uint32_t row      = lane_idx % 16;
        const uint32_t row_phy  = (row / 8) * 2 + (row % 8) / 2 * 4 + (row % 2) * 1;
        const uint32_t col      = (lane_idx / 16) * 16;
        const uint32_t v_offset = (row_phy / 4) * kNumBytesPer4Rows +
                                  ((row_phy % 4) * kNumElemPerRow + col) * sizeof(q_t);

        hkm::ds_read_b128<GPR_START>(p_lds + v_offset, 0);
    }

    // Get the size in bytes for a 16x64 block in LDS
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_per_block_in_byte()
    {
        // 16/4 * 272 = 1088
        static_assert(kNumElemPerCol % 4 == 0, "kNumElemPerCol must be divisible by 4!");
        return kNumElemPerCol / 4 * kNumBytesPer4Rows;
    }

    public:
    __device__ QManager8bitsV4() {}

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    { return T::kNumWarps * get_lds_size_per_block_in_byte(); }

    template <uint32_t GPR_NOPE_START, uint32_t GPR_ROPE_START>
    __device__ __forceinline__ void load_q_to_gpr(const typename T::gl_q& q_buffer,
                                                  const int32_t warp_idx,
                                                  const int32_t q_start,
                                                  const uintptr_t p_lds)
    {
        const uint32_t lane_idx = opus::lane_id();

        const uintptr_t p_lds_warp = p_lds + warp_idx * get_lds_size_per_block_in_byte();
        const q_t* p_q_buffer_warp =
            &q_buffer[{q_start, 0, 0, 0}] + warp_idx * kNumElemPerCol * T::kQkHeadDim;

        vram_2_lds<0>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<64>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 4>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<128>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 8>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<192>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 12>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<256>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 16>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<320>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 20>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<384>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 24>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<448>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_NOPE_START + 28>(p_lds_warp);
        asm volatile("s_waitcnt lgkmcnt(0)");
        vram_2_lds<512>(p_q_buffer_warp, p_lds_warp);
        asm volatile("s_waitcnt vmcnt(0)");
        lds_2_gpr<GPR_ROPE_START>(p_lds_warp);
    }
};

// Compared with V4, V5 uses 3 LDS buffers to load Q to reduce barrier & waitcnt time.
template <typename T>
class QManager8bitsV5 : public QManager8bitsV4<T>
{
    private:
    using q_t = typename T::q_t;

    public:
    __device__ QManager8bitsV5() : QManager8bitsV4<T>() {}

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        // using 3 buffers
        return 3 * T::kNumWarps * QManager8bitsV4<T>::get_lds_size_per_block_in_byte();
    }

    template <uint32_t GPR_NOPE_START, uint32_t GPR_ROPE_START>
    __device__ __forceinline__ void load_q_to_gpr(const typename T::gl_q& q_buffer,
                                                  const int32_t warp_idx,
                                                  const int32_t q_start,
                                                  const uintptr_t p_lds)
    {
        const uintptr_t p_lds_warp_0 =
            p_lds + 3 * warp_idx * QManager8bitsV4<T>::get_lds_size_per_block_in_byte();
        const uintptr_t p_lds_warp_1 =
            p_lds_warp_0 + QManager8bitsV4<T>::get_lds_size_per_block_in_byte();
        const uintptr_t p_lds_warp_2 =
            p_lds_warp_1 + QManager8bitsV4<T>::get_lds_size_per_block_in_byte();
        const q_t* p_q_buffer_warp = &q_buffer[{q_start, 0, 0, 0}] +
                                     warp_idx * QManager8bitsV4<T>::kNumElemPerCol * T::kQkHeadDim;

        this->template vram_2_lds<0>(p_q_buffer_warp, p_lds_warp_0);
        this->template vram_2_lds<64>(p_q_buffer_warp, p_lds_warp_1);
        asm volatile("s_waitcnt vmcnt(4)");
        this->template vram_2_lds<128>(p_q_buffer_warp, p_lds_warp_2);
        this->template lds_2_gpr<GPR_NOPE_START>(p_lds_warp_0);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<192>(p_q_buffer_warp, p_lds_warp_0);
        this->template lds_2_gpr<GPR_NOPE_START + 4>(p_lds_warp_1);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<256>(p_q_buffer_warp, p_lds_warp_1);
        this->template lds_2_gpr<GPR_NOPE_START + 8>(p_lds_warp_2);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<320>(p_q_buffer_warp, p_lds_warp_2);
        this->template lds_2_gpr<GPR_NOPE_START + 12>(p_lds_warp_0);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<384>(p_q_buffer_warp, p_lds_warp_0);
        this->template lds_2_gpr<GPR_NOPE_START + 16>(p_lds_warp_1);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<448>(p_q_buffer_warp, p_lds_warp_1);
        this->template lds_2_gpr<GPR_NOPE_START + 20>(p_lds_warp_2);
        asm volatile("s_waitcnt vmcnt(4), lgkmcnt(0)");
        this->template vram_2_lds<512>(p_q_buffer_warp, p_lds_warp_2);
        this->template lds_2_gpr<GPR_NOPE_START + 24>(p_lds_warp_0);
        asm volatile("s_waitcnt vmcnt(4)");
        this->template lds_2_gpr<GPR_NOPE_START + 28>(p_lds_warp_1);
        asm volatile("s_waitcnt vmcnt(0)");
        this->template lds_2_gpr<GPR_ROPE_START>(p_lds_warp_2);
    }
};

template <typename T>
class KvManager8bitsV1
{
    private:
    using kv_t = typename T::kv_t;

    /// TODO: These parameters should reside in Traits.
    // In the view of thread block on loading
    static constexpr uint32_t kNumRows = 32;
    static constexpr uint32_t kNumCols = 64;
    // In the view of warp on loading
    static constexpr uint32_t kNumColsPerWarp = kNumCols / T::kNumWarps;    // 64/8=8
    static constexpr uint32_t kNumElemPerWarp = kNumRows * kNumColsPerWarp; // 32*8=256
    static constexpr uint32_t kNumPaddingDw   = 4;                          // Skip 4 banks.
    static constexpr uint32_t kWarpOffset =
        kNumElemPerWarp * sizeof(kv_t) + kNumPaddingDw * sizeof(uint32_t); // 256*1+4*4=272
    static constexpr uint32_t kNumRowThreads = 32; // #threads handle the same column.
    static constexpr uint32_t kNumColThreads =
        opus::get_warp_size() / kNumRowThreads; // #threads handle the same row. 64/32=2
    static constexpr uint32_t kNumBytesPerThrPerRnd =
        4; // use buffer_load_dword which loads 4B each time.

    public:
    // LDS size in bytes for the whole 32 x kQkHeadDim KV block (one tile).
    // Layout is sliced into kQkHeadDim/kNumColsPerWarp = 72 (576/8) per-warp 32x8 strips,
    // each strip occupying kWarpOffset(=272) bytes including 2 DW padding.
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    { return kWarpOffset * (T::kQkHeadDim / kNumColsPerWarp); }

    __device__ __forceinline__ static uint32_t get_kv_ld_row_base_idx(const int32_t warp_idx)
    {
        const uint32_t lane_idx = opus::lane_id();
        return lane_idx / 2;
    }

    __device__ __forceinline__ static uint32_t get_kv_ld_col_base(const int32_t warp_idx)
    {
        const uint32_t lane_idx = opus::lane_id();
        return warp_idx * 8 + (lane_idx % 2) * 4;
    }

    __device__ __forceinline__ static uintptr_t get_p_lds_kv_warp_base(const int32_t warp_idx,
                                                                       const uintptr_t p_lds_kv)
    { return p_lds_kv + warp_idx * kWarpOffset; }

    // Load 32x64 elements from VRAM to LDS
    // Each warp loads 32x8 elements. Padding 2DW between 32x8 blocks.
    // After loading, the elements are in the following layout:
    // [0, 0-7], [1, 0-7], ..., [31, 0-7], 2 DW padding (by warp 0)
    // [0, 8-15], [1, 8-15], ..., [31, 8-15], 2 DW padding (by warp 1)
    // ...
    // [0, 56-63], [1, 56-63], ..., [31, 56-63], 2 DW padding (by warp 7)
    // ...
    // [0, 504-511], [1, 504-511], ..., [31, 504-511], 2 DW padding (by warp 7)
    // ...
    // [0, 568-575], [1, 568-575], ..., [31, 568-575]  (by warp 7)
    //
    // @param p_lds_kv_warp_base here is expected to be the start address of the warp:
    //        p_lds_kv + warp_idx * kWarpOffset(272).
    // @param row: the row index loaded from p_kv_indices.
    // @param col_base: the base column index which should be:
    //        warp_idx * kNumColsPerWarp(8) + lane_idx % kNumColThreads(2) *
    //        kNumBytesPerThrPerRnd(4)
    template <uint32_t kRowOffset,
              uint32_t kColOffset,
              bool kIsLastIter,
              bool kCheckBoundary = true>
    __device__ __forceinline__ static void async_load_k_tile(const uintptr_t p_lds_kv_warp_base,
                                                             const uint32_t warp_idx,
                                                             const typename T::gl_kv& kv_buffer,
                                                             const int32_t row,
                                                             const int32_t col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            static_assert(((kColOffset % 64) == 0) && (kColOffset < 576),
                          "async_load_k(): Unsupported column offset!");
            static_assert(kRowOffset == 0,
                          "KvManager8bitsV1::async_load_k_tile(): kRowOffset must be 0");

            const uint32_t lane_idx = opus::lane_id();

            const uintptr_t p_lds_kv_warp =
                p_lds_kv_warp_base + kColOffset / kNumColsPerWarp * kWarpOffset - kColOffset;

            if(kCheckBoundary && (row == -1))
            {
                const uintptr_t p_lds_kv_lane =
                    p_lds_kv_warp + kColOffset + lane_idx * kNumBytesPerThrPerRnd;
                hkm::ds_write_b32(0u, p_lds_kv_lane, 0);
            }
            else
            {
                const kv_t* p_kv_buffer = &kv_buffer[{0, 0, 0, 0}];
                const hk::i32x4 srsrc   = hk::make_srsrc(p_kv_buffer, 0xffffffff);

                const uint32_t voffset = row * T::kQkHeadDim * sizeof(kv_t) + col_base;

                hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                                    (hk::as3_uint32_ptr)(p_lds_kv_warp),
                                                    kNumBytesPerThrPerRnd,
                                                    voffset,
                                                    0,
                                                    kColOffset,
                                                    0);
            }
        }
    }

    template <uint32_t kRowOffset, bool kIsLastIter, bool kCheckBoundary>
    __device__ __forceinline__ static void async_load_k(const uintptr_t p_lds_kv,
                                                        const uint32_t warp_idx,
                                                        const typename T::gl_kv& kv_buffer,
                                                        const int32_t row_kv_ld,
                                                        const int32_t kv_ld_col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            const uintptr_t p_lds_kv_warp = get_p_lds_kv_warp_base(warp_idx, p_lds_kv);

            async_load_k_tile<kRowOffset, 0, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 64, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 128, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 192, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 256, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 320, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 384, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 448, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 512, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
        }
    }

    // Load 16x32 blocks from LDS to GPR. Each thread takes contiguous 8 elements.
    template <uint32_t kRowOffset, uint32_t kColOffset, hkdart::all RT>
    __device__ __forceinline__ static void load_k_to_gpr(RT& dst, const uintptr_t p_lds_kv)
    {
        constexpr uint32_t kMfmaRows = 16; // 16 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaCols = 32; // 32 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaElemPerThr =
            kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*32/64=8

        static_assert(((kRowOffset % 16) == 0) && (kRowOffset < 32),
                      "load_k_to_gpr(): Unsupported row offset!");
        static_assert(((kColOffset % 32) == 0) && (kColOffset < 576),
                      "load_k_to_gpr(): Unsupported column offset!");

        const uint32_t lane_idx = opus::lane_id();

        // // equivalent with kFixedOffset=0
        // const uint32_t row = kRowOffset + lane_idx % kMfmaRows;
        // const uint32_t col = kColOffset + lane_idx / kMfmaRows * kMfmaElemPerThr;
        // const uintptr_t p_lds_kv_lane =
        //     p_lds_kv + row * kMfmaElemPerThr * sizeof(kv_t) + (col / kNumColsPerWarp) *
        //     kWarpOffset;
        // constexpr uint32_t kFixedOffset = 0;

        const uint32_t row = lane_idx % kMfmaRows;
        const uint32_t col = lane_idx / kMfmaRows * kMfmaElemPerThr;
        const uintptr_t p_lds_kv_lane =
            p_lds_kv + row * kMfmaElemPerThr * sizeof(kv_t) + col / kNumColsPerWarp * kWarpOffset;
        constexpr uint32_t kFixedOffset = kRowOffset * kMfmaElemPerThr * sizeof(kv_t) +
                                          kColOffset / kNumColsPerWarp * kWarpOffset;

        // RT must hold exactly one 2-vgpr range (one mfma A-tile). Caller passes the
        // appropriate sub-view per kRowOffset; the function always writes to range 0.
        using range_type = hkdart::get_nth_range_t<typename RT::register_ranges, 0>;
        static_assert(range_type::lo + 1 == range_type::hi,
                      "ds_read_b64 requires 2 consecutive registers");
        hkm::ds_read_b64<range_type::lo>(p_lds_kv_lane, kFixedOffset);
    }

    // Load un-transposed vector from LDS to GPR.
    __device__ __forceinline__ static void
    load_v_to_gpr(v8ui* p_result, const uint32_t warp_idx, const uintptr_t p_lds_v)
    {
        const uint32_t lane_idx = opus::lane_id();

        // Each warp takes 16x128 elements. Each thread takes 4x8 elements block-wise column-major
        // layout.
        const uint32_t row = (warp_idx % 2) * 16 + lane_idx / 16 * 4;
        const uint32_t col = (lane_idx % 16) * 8 + warp_idx / 2 * 128;

        const uintptr_t p_lds_v_lane =
            p_lds_v + row * 8 * sizeof(kv_t) +
            col / kNumColsPerWarp * kWarpOffset /*+ col % kNumColsPerWarp * sizeof(kv_t)*/;

        const v4ui pass_0 = hkm::ds_read_b128(p_lds_v_lane, 0);
        const v4ui pass_1 = hkm::ds_read_b128(p_lds_v_lane, 4 * sizeof(uint32_t));

        *p_result = {
            pass_0.x, pass_0.y, pass_0.z, pass_0.w, pass_1.x, pass_1.y, pass_1.z, pass_1.w};
    }
};

template <typename T>
class KvManager8bitsV2
{
    private:
    using kv_t = typename T::kv_t;

    /// TODO: These parameters should reside in Traits.
    // In the view of thread block on loading
    static constexpr uint32_t kNumRows            = 32;
    static constexpr uint32_t kNumCols            = 64;
    static constexpr uint32_t kNumRowsPerSubBlock = kNumRows / T::kNumWarps;  // 32/8=4
    static constexpr uint32_t kNumBlocks          = T::kQkHeadDim / kNumCols; // 576/64=9
    static constexpr uint32_t kNumPaddingDw       = 2; // 2 DW padding between each sub-block.
    static constexpr uint32_t kNumBytesPerRow     = kNumCols * sizeof(kv_t); // 64*1=64
    static constexpr uint32_t kNumBytesPerSubBlock =
        kNumRowsPerSubBlock * kNumBytesPerRow + kNumPaddingDw * sizeof(uint32_t); // 4*64*1+2*4=264
    static constexpr uint32_t kNumSubBlocks = kNumRows / kNumRowsPerSubBlock;     // 32/4=8
    static constexpr uint32_t kNumBytesPerBlock =
        kNumBytesPerSubBlock * kNumSubBlocks; // 264*8=2112
    static constexpr uint32_t kNumBytesPerThrPerRnd =
        4; // use buffer_load_dword which loads 4B each time.

    static_assert(T::kQkHeadDim % kNumCols == 0, "kQkHeadDim must be divisible by kNumCols!");

    public:
    // There are 576 / 64 = 9 blocks. Each block contains 32x64 elements.
    // There are 32 / 4 = 8 sub-blocks. Each sub-block contains 4x64 elements.
    // There are 2 DW padding between each sub-block.
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return kNumBytesPerBlock * kNumBlocks; // 2112*9=19008
    }

    // Each warp takes 4 rows, each row is handled by 16 contiguous threads:
    //   warp[0]: row[ 0- 1], row[16-17], warp[1]: row[ 2- 3], row[18-19]
    //   warp[2]: row[ 4- 5], row[20-21], warp[3]: row[ 6- 7], row[22-23]
    //   warp[4]: row[ 8- 9], row[24-25], warp[5]: row[10-11], row[26-27]
    //   warp[6]: row[12-13], row[28-29], warp[7]: row[14-15], row[30-31]
    __device__ __forceinline__ static uint32_t get_kv_ld_row_base_idx(const int32_t warp_idx)
    {
        constexpr uint32_t kNumRowsPerWarp     = 4;                   // 4 rows per warp.
        constexpr uint32_t kNumRowGroupPerWarp = kNumRowsPerWarp / 2; // 4 / 2 = 2
        constexpr uint32_t kNumRowsPerRowGroup = kNumRowsPerWarp / kNumRowGroupPerWarp; // 4 / 2 = 2
        constexpr uint32_t kRowGroupStride     = kNumRows / kNumRowGroupPerWarp; // 32 / 2 = 16
        constexpr uint32_t kNumThreadsPerRowGroup =
            opus::get_warp_size() / kNumRowGroupPerWarp; // 64 / 2 = 32

        const uint32_t lane_idx = opus::lane_id();
        // (lane_idx / 32) * 16 + (lane_idx / 16) % 2 + warp_idx * 2
        return (lane_idx / kNumThreadsPerRowGroup) * kRowGroupStride +
               (lane_idx / kRowGroupStride) % kNumRowsPerRowGroup + warp_idx * kNumRowsPerRowGroup;
    }

    __device__ __forceinline__ static uint32_t get_kv_ld_col_base(const int32_t warp_idx)
    {
        const uint32_t lane_idx = opus::lane_id();
        return (lane_idx % 16) * 4;
    }

    __device__ __forceinline__ static uintptr_t get_p_lds_kv_warp_base(const int32_t warp_idx,
                                                                       const uintptr_t p_lds_kv)
    { return p_lds_kv + warp_idx * kNumBytesPerSubBlock; }

    // Load 32x64 elements from VRAM to LDS
    // Each warp loads 4x64 elements. Padding 2DW between 4x64 blocks.
    // After loading, the elements are in the following layout:
    // (00, 000 - 063) [W0L00 - W0L15] BANK 00-15
    // (01, 000 - 063) [W0L16 - W0L31] BANK 16-31
    // (16, 000 - 063) [W0L32 - W0L47] BANK 00-15
    // (17, 000 - 063) [W0L48 - W0L63] BANK 16-31
    // 2DW padding
    // (02, 000 - 063) [W1L00 - W1L15] BANK 02-17
    // (03, 000 - 063) [W1L16 - W1L31] BANK 18-01
    // (18, 000 - 063) [W1L32 - W1L47] BANK 02-17
    // (19, 000 - 063) [W1L48 - W1L63] BANK 18-01
    // 2DW padding
    // ...
    // (14, 000 - 063) [W7L00 - W7L15] BANK 14-29
    // (15, 000 - 063) [W7L16 - W7L31] BANK 30-13
    // (30, 000 - 063) [W7L32 - W7L47] BANK 14-29
    // (31, 000 - 063) [W7L48 - W7L63] BANK 30-13
    // 2DW padding
    template <uint32_t kRowOffset,
              uint32_t kColOffset,
              bool kIsLastIter,
              bool kCheckBoundary = true>
    __device__ __forceinline__ static void async_load_k_tile(const uintptr_t p_lds_kv_warp_base,
                                                             const uint32_t warp_idx,
                                                             const typename T::gl_kv& kv_buffer,
                                                             const int32_t row,
                                                             const int32_t col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            static_assert(((kColOffset % 64) == 0) && (kColOffset < 576),
                          "async_load_k(): Unsupported column offset!");
            static_assert(kRowOffset == 0,
                          "KvManager8bitsV2::async_load_k_tile(): kRowOffset must be 0");

            constexpr uint32_t kBlockIdx = kColOffset / 64;

            const uint32_t lane_idx = opus::lane_id();

            const uintptr_t p_lds_kv_warp =
                p_lds_kv_warp_base + kBlockIdx * kNumBytesPerBlock - kColOffset;

            if(kCheckBoundary && (row == -1))
            {
                const uintptr_t p_lds_kv_lane =
                    p_lds_kv_warp + kColOffset + lane_idx * kNumBytesPerThrPerRnd;
                hkm::ds_write_b32(0u, p_lds_kv_lane, 0);
            }
            else
            {
                const kv_t* p_kv_buffer = &kv_buffer[{0, 0, 0, 0}];
                const hk::i32x4 srsrc   = hk::make_srsrc(p_kv_buffer, 0xffffffff);

                const uint32_t voffset = row * T::kQkHeadDim * sizeof(kv_t) + col_base;

                hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                                    (hk::as3_uint32_ptr)(p_lds_kv_warp),
                                                    kNumBytesPerThrPerRnd,
                                                    voffset,
                                                    0,
                                                    kColOffset,
                                                    0);
            }
        }
    }

    template <uint32_t kRowOffset, bool kIsLastIter, bool kCheckBoundary>
    __device__ __forceinline__ static void async_load_k(const uintptr_t p_lds_kv,
                                                        const uint32_t warp_idx,
                                                        const typename T::gl_kv& kv_buffer,
                                                        const int32_t row_kv_ld,
                                                        const int32_t kv_ld_col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            const uintptr_t p_lds_kv_warp = get_p_lds_kv_warp_base(warp_idx, p_lds_kv);

            async_load_k_tile<kRowOffset, 0, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 64, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 128, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 192, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 256, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 320, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 384, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 448, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 512, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
        }
    }

    // Load 16x32 blocks from LDS to GPR. Each thread takes contiguous 8 elements.
    template <uint32_t kRowOffset, uint32_t kColOffset, hkdart::all RT>
    __device__ __forceinline__ static void load_k_to_gpr(RT& dst, const uintptr_t p_lds_kv)
    {
        constexpr uint32_t kMfmaRows = 16; // 16 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaCols = 32; // 32 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaElemPerThr =
            kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*32/64=8

        static_assert(((kRowOffset % 16) == 0) && (kRowOffset < 32),
                      "load_k_to_gpr(): Unsupported row offset!");
        static_assert(((kColOffset % 32) == 0) && (kColOffset < 576),
                      "load_k_to_gpr(): Unsupported column offset!");

        // Canonical address (matches load_v_to_gpr() / store layout):
        //   row     = kRowOffset + lane_idx % kMfmaRows;             // ? [kRowOffset,
        //   kRowOffset+16) row_phy = ((row % 16) / 2) * 4 + 2 * (row / 16) + (row % 2); col     =
        //   kColOffset + (lane_idx / kMfmaRows) * kMfmaElemPerThr; p_lds_kv_lane = p_lds_kv +
        //       (row_phy / 4)         * kNumBytesPerSubBlock +
        //       (row_phy % 4)         * kNumBytesPerRow +
        //        col / kNumCols       * kNumBytesPerBlock +
        //       (col % kNumCols)      * sizeof(kv_t);
        //
        // Per-lane simplifications (lane row ? [0,16), lane col ? {0,8,16,24}):
        //   row/16 == 0          => row_phy = (row/2)*4 + (row%2)
        //                        => row_phy/4 == row/2, row_phy%4 == row%2
        //   col < 32 < kNumCols  => col/kNumCols == 0, col%kNumCols == col
        // kRowOffset/kColOffset terms are constexpr-folded into kFixedOffset.
        // kRowOffset==16 shifts row_phy by +2 (always lands in row_phy%4),
        // contributing +(kRowOffset/16) * 2 * kNumBytesPerRow.
        const uint32_t lane_idx         = opus::lane_id();
        const uint32_t row              = lane_idx % kMfmaRows;
        const uint32_t col              = (lane_idx / kMfmaRows) * kMfmaElemPerThr;
        const uintptr_t p_lds_kv_lane   = p_lds_kv + (row / 2) * kNumBytesPerSubBlock +
                                          (row % 2) * kNumBytesPerRow + col * sizeof(kv_t);
        constexpr uint32_t kFixedOffset = (kRowOffset / 16) * 2 * kNumBytesPerRow +
                                          (kColOffset / kNumCols) * kNumBytesPerBlock +
                                          (kColOffset % kNumCols) * sizeof(kv_t);

        // RT must hold exactly one 2-vgpr range (one mfma A-tile). Caller passes the
        // appropriate sub-view per kRowOffset; the function always writes to range 0.
        using range_type = hkdart::get_nth_range_t<typename RT::register_ranges, 0>;
        static_assert(range_type::lo + 1 == range_type::hi,
                      "ds_read_b64 requires 2 consecutive registers");
        hkm::ds_read_b64<range_type::lo>(p_lds_kv_lane, kFixedOffset);
    }

    // Load un-transposed vector from LDS to GPR.
    __device__ __forceinline__ static void
    load_v_to_gpr(v8ui* p_result, const uint32_t warp_idx, const uintptr_t p_lds_v)
    {
        const uint32_t lane_idx = opus::lane_id();

        // Each warp takes 16x128 elements. Each thread takes 4x8 elements block-wise column-major
        // layout.
        const uint32_t row     = (warp_idx % 2) * 16 + lane_idx / 16 * 4;
        const uint32_t row_phy = ((row % 16) / 2) * 4 + 2 * (row / 16) + (row % 2);
        const uint32_t col     = (lane_idx % 16) * 8 + warp_idx / 2 * 128;

        const uintptr_t p_lds_v_lane =
            p_lds_v + (row_phy / 4) * kNumBytesPerSubBlock + (row_phy % 4) * kNumBytesPerRow +
            (col / kNumCols) * kNumBytesPerBlock + (col % kNumCols) * sizeof(kv_t);

        const v2ui pass_0 = hkm::ds_read_b64(p_lds_v_lane, 0);
        const v2ui pass_1 = hkm::ds_read_b64(p_lds_v_lane, kNumBytesPerRow);
        const v2ui pass_2 = hkm::ds_read_b64(p_lds_v_lane, kNumBytesPerSubBlock);
        const v2ui pass_3 = hkm::ds_read_b64(p_lds_v_lane, kNumBytesPerSubBlock + kNumBytesPerRow);

        *p_result = {
            pass_0.x, pass_0.y, pass_1.x, pass_1.y, pass_2.x, pass_2.y, pass_3.x, pass_3.y};
    }
};

template <typename T>
class KvManager8bitsV3
{
    private:
    using kv_t = typename T::kv_t;

    /// TODO: These parameters should reside in Traits.
    // In the view of thread block on loading
    static constexpr uint32_t kNumRows         = T::kBlockN;
    static constexpr uint32_t kNumCols         = 64;
    static constexpr uint32_t kNumSubBlockRows = 4;
    static constexpr uint32_t kNumSubBlockCols = 32;
    static constexpr uint32_t kNumBlocks       = T::kQkHeadDim / kNumCols; // 576/64=9
    static constexpr uint32_t kNumPaddingDw    = 2;
    static constexpr uint32_t kNumBytesPerSubBlock =
        kNumSubBlockRows * kNumSubBlockCols * sizeof(kv_t); // 4*32*1=128
    static constexpr uint32_t kNumBytesPer2SubBlocksWithPadding =
        kNumBytesPerSubBlock * 2 + kNumPaddingDw * sizeof(uint32_t); // 128*2+2*4=264
    // LDS layout: kBlockN x 64 block split into kBlockN/4 sub-block slots; INDEPENDENT of
    // kNumWarps.
    static constexpr uint32_t kNum2SubBlocks = kNumRows / 4; // kBlockN=32 -> 8; kBlockN=64 -> 16
    static_assert(kNum2SubBlocks % T::kNumWarps == 0,
                  "kNum2SubBlocks must be a multiple of kNumWarps");
    static constexpr uint32_t kNumPassesPerWarp = kNum2SubBlocks / T::kNumWarps; // 1 or 2
    static constexpr uint32_t kNumBytesPerBlock =
        kNumBytesPer2SubBlocksWithPadding * kNum2SubBlocks;           // 264 * kNum2SubBlocks
    static constexpr uint32_t kNumRowsPerWarp = kNumSubBlockRows * 2; // 8
    static constexpr uint32_t kNumWarpsPerCol = 32 / kNumRowsPerWarp; // 4 (rows per pass / 8)
    // Slot stride between consecutive row-passes within a col-block. Equals
    // kNumWarpsPerCol * kNumColStripsPerBlock = 4 * 2 = 8 slots, i.e. one full row-pass
    // covers all warp-rows x all col-strips before the next row-pass begins. Constant
    // across kNumWarps so row-strip and col-strip slot offsets stay independent (col-strip
    // stride is 4 slots; row-strip stride must differ to avoid collision when both are used,
    // as in m16x4 kBlockN=64).
    static constexpr uint32_t kRowPassSlotStride = kNumWarpsPerCol * 2; // 8
    static constexpr uint32_t kNumBytesPerThrPerRnd =
        4; // use buffer_load_dword which loads 4B each time.
    static constexpr uint32_t kNumThrPerSubBlockRow =
        kNumSubBlockCols / kNumBytesPerThrPerRnd; // 32 / 4 = 8

    static_assert(T::kQkHeadDim % kNumCols == 0, "kQkHeadDim must be divisible by kNumCols!");

    // Per-lane LDS byte offset within a 32-row x 32-col sub-tile of one warp's V/K block.
    // Shared by load_k_to_gpr() and load_transposed_v_to_gpr(): both walk a 16x32 tile,
    // and per-lane (row, col) lands in the same place -- only the rule that maps lane_idx
    // to (row, col) differs (mfma A-tile layout vs ds_read_b64_tr_b8 input footprint).
    //
    // Preconditions (caller must guarantee):
    //   row ? [0, 16)         -- local row inside the 16-row tile.
    //   col ? {0, 8, 16, 24}  -- local col inside the 32-col sub-block.
    // With those, the canonical formula
    //   (row_phy/8)*264 + (row_phy%8)*32 + col/64*2112 + (col%64)/32*1056 + (col%64)%32
    // collapses to the two terms below (see load_*_to_gpr() comments for the derivation).
    __device__ __forceinline__ static uint32_t get_block_lane_offset(const uint32_t row,
                                                                     const uint32_t col)
    {
        return (row / 4) * kNumBytesPer2SubBlocksWithPadding +
               ((row % 4) * kNumSubBlockCols + col) * sizeof(kv_t);
    }

    // Constexpr ds_read immediate-offset that selects the (kRowOffset, kColOffset)
    // sub-tile within the warp's V/K block.
    //   kRowOffset ? {0, 16, 32, 48}                  -- top/bot 16-row sub-tile of each pass.
    //                                                    (For kBlockN=32 only 0/16 valid.)
    //   kColOffset is a multiple of 32, < kQkHeadDim -- picks the 32-col strip.
    // Layout B (per 64-col block): pass 1 of all warps comes after pass 0 of all warps.
    //   pass = kRowOffset / 32                            -> +pass * kRowPassSlotStride * 264
    //   sub-block within pass = (kRowOffset % 32) / 16    -> +sub * 128
    //   64-col block index = kColOffset / 64              -> +block * kNumBytesPerBlock
    //   32-col strip within block = (kColOffset % 64) / 32 -> +strip * 4 * 264
    // Row-strip stride uses constant 8 (not T::kNumWarps) so that row and col strips occupy
    // independent slot bits: row -> slots {0,8}, col -> slots {0,4}. With kNumWarps=8 (m16x8)
    // this matches the original kNumWarps stride; with kNumWarps=4 (m16x4) it avoids the
    // collision where (row=32,col=0) and (row=0,col=32) would both land on slot+4.
    // The block stride must use kNumBytesPerBlock (which depends on kBlockN via
    // kNum2SubBlocks); collapsing it into (kColOffset/32)*4*264 only works when
    // kNum2SubBlocks == 8 (i.e., kBlockN == 32).
    template <uint32_t kRowOffset, uint32_t kColOffset>
    static constexpr uint32_t get_block_fixed_offset()
    {
        return (kRowOffset / 32) * kRowPassSlotStride * kNumBytesPer2SubBlocksWithPadding +
               ((kRowOffset % 32) / 16) * kNumBytesPerSubBlock +
               (kColOffset / 64) * kNumBytesPerBlock +
               ((kColOffset % 64) / 32) * 4 * kNumBytesPer2SubBlocksWithPadding;
    }

    public:
    // There are 576 / 64 = 9 blocks. Each block contains 32x64 elements.
    // The number of sub-blocks is 8. Each sub-block contains 2 blocks of 4x32 elements.
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return kNumBytesPerBlock * kNumBlocks; // 2112*9=19008
    }

    // Each warp takes two 4x32 blocks (rows r..r+3 and r+16..r+19); each row is handled by 8
    // contiguous threads. warps {0,4}/{1,5}/{2,6}/{3,7} differ only in column block; the row sets:
    // warp[0, 4]: row[ 0- 3], row[16-19]
    // warp[1, 5]: row[ 4- 7], row[20-23]
    // warp[2, 6]: row[ 8-11], row[24-27]
    // warp[3, 7]: row[12-15], row[28-31]
    __device__ __forceinline__ static uint32_t get_kv_ld_row_base_idx(const int32_t warp_idx)
    {
        constexpr uint32_t kNumThrPerSubBlock =
            kNumSubBlockRows * kNumSubBlockCols / kNumBytesPerThrPerRnd; // 4 * 32 / 4 = 32

        const uint32_t lane_idx = opus::lane_id();
        // (warp_idx % 4) * 4 + (lane_idx / 32) * 16 + (lane_idx % 32) / 8
        return (warp_idx % kNumWarpsPerCol) * kNumSubBlockRows +
               (lane_idx / kNumThrPerSubBlock) * kNumWarpsPerCol * kNumSubBlockRows +
               (lane_idx % kNumThrPerSubBlock) / kNumThrPerSubBlockRow;
    }

    __device__ __forceinline__ static uint32_t get_kv_ld_col_base(const int32_t warp_idx)
    {
        const uint32_t lane_idx = opus::lane_id();
        return (warp_idx / kNumWarpsPerCol) * kNumSubBlockCols +
               (lane_idx % kNumThrPerSubBlockRow) * kNumBytesPerThrPerRnd;
    }

    // Layout B: pass 1 of all warps lives after pass 0 of all warps. Callers requesting a
    // col-strip pass use `warp_idx + kNumWarps` (col offset = +4*264 in slot space); callers
    // requesting a row-strip pass use the kRowOffset=32 template arg in get_block_fixed_offset
    // and async_load_k_tile (row offset = +8*264 in slot space). m16x4 kBlockN=32 uses only
    // col-strip; m16x8 kBlockN=64 uses only row-strip; m16x4 kBlockN=64 uses both, packed
    // into the 16 available slots/col-block. Stride per warp slot = 264 bytes (one
    // 2-sub-block-with-padding).
    __device__ __forceinline__ static uintptr_t get_p_lds_kv_warp_base(const int32_t warp_idx,
                                                                       const uintptr_t p_lds_kv)
    { return p_lds_kv + warp_idx * kNumBytesPer2SubBlocksWithPadding; }

    // Load 32x64 elements from VRAM to LDS
    // Each warp loads two 4x32 elements. Padding 2DW between warps.
    // After loading, the elements are in the following layout:
    // (00, 000 - 031) [W0L00 - W0L07] BANK 00-07
    // (01, 000 - 031) [W0L08 - W0L15] BANK 08-15
    // (02, 000 - 031) [W0L16 - W0L23] BANK 16-23
    // (03, 000 - 031) [W0L24 - W0L31] BANK 24-31
    // (16, 000 - 031) [W0L32 - W0L39] BANK 00-07
    // (17, 000 - 031) [W0L40 - W0L47] BANK 08-15
    // (18, 000 - 031) [W0L48 - W0L55] BANK 16-23
    // (19, 000 - 031) [W0L56 - W0L63] BANK 24-31
    // 2DW padding
    // (04, 000 - 031) [W1L00 - W1L07] BANK 02-09
    // ...
    // (23, 000 - 031) [W1L56 - W1L63] BANK 26-01
    // 2DW padding
    // (08, 000 - 031) [W2L00 - W2L07] BANK 04-11
    // ...
    // (27, 000 - 031) [W2L56 - W2L63] BANK 28-03
    // 2DW padding
    // (12, 000 - 031) [W3L00 - W3L07] BANK 06-13
    // ...
    // (31, 000 - 031) [W3L56 - W3L63] BANK 30-05
    // 2DW padding
    // (00, 032 - 063) [W4L00 - W4L07] BANK 08-15
    // ...
    // (31, 032 - 063) [W7L56 - W7L63] BANK 06-13
    //
    // Single-pass loader: each call issues exactly one buffer_load_dword and writes
    // one 32x64 sub-tile into LDS. For kBlockN=64 (kNumPassesPerWarp=2) the caller
    // invokes this twice with kRowOffset=0,32; the kRowOffset=p*32 sub-tile covers
    // KV rows [kv_tile_start + p*32, kv_tile_start + (p+1)*32) and writes to LDS
    // slot warp_idx + p*kNumWarps within the column-block (Layout B).
    // `row` is the physical KV row already resolved by get_kv_ld_row (-1 means OOB).
    template <uint32_t kRowOffset,
              uint32_t kColOffset,
              bool kIsLastIter,
              bool kCheckBoundary = true>
    __device__ __forceinline__ static void async_load_k_tile(const uintptr_t p_lds_kv_warp_base,
                                                             const uint32_t warp_idx,
                                                             const typename T::gl_kv& kv_buffer,
                                                             const int32_t row,
                                                             const int32_t col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            static_assert(((kColOffset % 64) == 0) && (kColOffset < 576),
                          "async_load_k(): Unsupported column offset!");
            static_assert((kRowOffset == 0) || (kRowOffset == 32),
                          "async_load_k_tile(): kRowOffset must be 0 or 32");
            static_assert((kRowOffset / 32) < kNumPassesPerWarp,
                          "async_load_k_tile(): kRowOffset out of range for kBlockN");

            constexpr uint32_t kPass     = kRowOffset / 32;
            constexpr uint32_t kBlockIdx = kColOffset / 64;

            const uint32_t lane_idx = opus::lane_id();

            const kv_t* p_kv_buffer = &kv_buffer[{0, 0, 0, 0}];
            const hk::i32x4 srsrc   = hk::make_srsrc(p_kv_buffer, 0xffffffff);

            const uintptr_t p_lds_kv_warp =
                p_lds_kv_warp_base +
                kPass * kRowPassSlotStride * kNumBytesPer2SubBlocksWithPadding +
                kBlockIdx * kNumBytesPerBlock - kColOffset;

            if(kCheckBoundary && (row == -1))
            {
                const uintptr_t p_lds_kv_lane =
                    p_lds_kv_warp + kColOffset + lane_idx * kNumBytesPerThrPerRnd;
                hkm::ds_write_b32(0u, p_lds_kv_lane, 0);
            }
            else
            {
                const uint32_t voffset = row * T::kQkHeadDim * sizeof(kv_t) + col_base;

                hk::llvm_amdgcn_raw_buffer_load_lds(srsrc,
                                                    (hk::as3_uint32_ptr)(p_lds_kv_warp),
                                                    kNumBytesPerThrPerRnd,
                                                    voffset,
                                                    0,
                                                    kColOffset,
                                                    0);
            }
        }
    }

    // Single-pass bulk loader: loads one 32x576 row-stripe (9 column blocks) into LDS.
    // For kBlockN=32 the caller invokes this once with kRowOffset=0; for kBlockN=64
    // the caller invokes it twice with kRowOffset=0 and kRowOffset=32, supplying the
    // physical KV row for each pass.
    template <uint32_t kRowOffset, bool kIsLastIter, bool kCheckBoundary>
    __device__ __forceinline__ static void async_load_k(const uintptr_t p_lds_kv,
                                                        const uint32_t warp_idx,
                                                        const typename T::gl_kv& kv_buffer,
                                                        const int32_t row_kv_ld,
                                                        const int32_t kv_ld_col_base)
    {
        if constexpr(kIsLastIter == false)
        {
            const uintptr_t p_lds_kv_warp = get_p_lds_kv_warp_base(warp_idx, p_lds_kv);

            async_load_k_tile<kRowOffset, 0, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 64, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 128, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 192, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 256, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 320, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 384, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 448, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
            async_load_k_tile<kRowOffset, 512, false, kCheckBoundary>(
                p_lds_kv_warp, warp_idx, kv_buffer, row_kv_ld, kv_ld_col_base);
        }
    }

    // Load 16x32 blocks from LDS to GPR. Each thread takes contiguous 8 elements.
    template <uint32_t kRowOffset, uint32_t kColOffset, hkdart::all RT>
    __device__ __forceinline__ static void load_k_to_gpr(RT& dst, const uintptr_t p_lds_kv)
    {
        constexpr uint32_t kMfmaRows = 16; // 16 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaCols = 32; // 32 refers to mfma_f32_16x16x32_fp8_fp8.
        constexpr uint32_t kMfmaElemPerThr =
            kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*32/64=8

        static_assert(((kRowOffset % 16) == 0) && (kRowOffset < T::kBlockN),
                      "load_k_to_gpr(): Unsupported row offset!");
        static_assert(((kColOffset % 32) == 0) && (kColOffset < 576),
                      "load_k_to_gpr(): Unsupported column offset!");

        // Per-lane (row, col): mfma_f32_16x16x32 A-tile layout.
        //   row = lane_idx % 16    ? [0, 16)
        //   col = (lane_idx / 16) * 8 ? {0, 8, 16, 24}
        // See get_block_lane_offset() / get_block_fixed_offset() for the address math.
        const uint32_t lane_idx         = opus::lane_id();
        const uint32_t row              = lane_idx % kMfmaRows;
        const uint32_t col              = (lane_idx / kMfmaRows) * kMfmaElemPerThr;
        const uintptr_t p_lds_kv_lane   = p_lds_kv + get_block_lane_offset(row, col);
        constexpr uint32_t kFixedOffset = get_block_fixed_offset<kRowOffset, kColOffset>();

        // RT must hold exactly one 2-vgpr range (one mfma A-tile = 16x32 = 2 vgprs).
        // Caller passes the appropriate sub-view per kRowOffset; the function always
        // writes to range 0. This decouples the destination VGPR from the LDS source
        // address (selected by kFixedOffset via kRowOffset, including pass bits for
        // the upper N-half on kBlockN=64).
        using range_type = hkdart::get_nth_range_t<typename RT::register_ranges, 0>;
        static_assert(range_type::lo + 1 == range_type::hi,
                      "ds_read_b64 requires 2 consecutive registers");
        hkm::ds_read_b64<range_type::lo>(p_lds_kv_lane, kFixedOffset);
    }

    // Load un-transposed vector from LDS to GPR.
    // Each warp takes (kNumRows/2) x 128 elements: per-thread 4x8 block-wise column-major layout.
    // For kBlockN=64 (kNumSubTiles=2), writes 2 consecutive v8ui (sub-tile 0: rows R..R+3,
    // sub-tile 1: rows R+32..R+35). Caller must allocate p_result[kNumSubTiles].
    __device__ __forceinline__ static void
    load_v_to_gpr(v8ui* p_result, const uint32_t warp_idx, const uintptr_t p_lds_v)
    {
        const uint32_t lane_idx         = opus::lane_id();
        constexpr uint32_t kNumSubTiles = kNumRows / 32;
        const uint32_t col              = (lane_idx % 16) * 8 + warp_idx / 2 * 128;

#pragma unroll
        for(uint32_t sub = 0; sub < kNumSubTiles; ++sub)
        {
            const uint32_t row = (warp_idx % 2) * 16 + lane_idx / 16 * 4 + sub * 32;
            // Layout-B row_phy: linear LDS slot ID = pass * kNumWarps + warp_for_row,
            // then 8 row_phy units per slot (sub_block * 4 + sub_row).
            //   warp_for_row = (row % 16) / 4
            //   pass         = row / 32
            //   sub_block    = (row % 32) / 16
            //   sub_row      = row % 4
            const uint32_t row_phy = ((row / 32) * kRowPassSlotStride + (row % 16) / 4) * 8 +
                                     ((row % 32) / 16) * 4 + (row % 4);
            const uintptr_t p_lds_v_lane =
                p_lds_v + (row_phy / 8) * kNumBytesPer2SubBlocksWithPadding +
                (row_phy % 8) * kNumSubBlockCols * sizeof(kv_t) +
                col / kNumCols * kNumBytesPerBlock +
                (col % kNumCols) / 32 * (4 * kNumBytesPer2SubBlocksWithPadding) +
                ((col % kNumCols) % 32) * sizeof(kv_t);

            const v2ui pass_0 = hkm::ds_read_b64(p_lds_v_lane, 0);
            const v2ui pass_1 = hkm::ds_read_b64(p_lds_v_lane, 32);
            const v2ui pass_2 = hkm::ds_read_b64(p_lds_v_lane, 64);
            const v2ui pass_3 = hkm::ds_read_b64(p_lds_v_lane, 96);

            p_result[sub] = {
                pass_0.x, pass_0.y, pass_1.x, pass_1.y, pass_2.x, pass_2.y, pass_3.x, pass_3.y};
        }
    }

    // Load a 16x32 (rows x cols) tile of V from LDS into 2 consecutive GPRs per lane,
    // transposed for use as the B operand of mfma_f32_16x16x32_fp8_fp8.
    //
    // The 64-lane wave is split into 4 lane groups of 16 lanes. Each group handles a
    // 4x32 sub-tile (rows r..r+3, cols 0..31 in tile-local coords). Within a group,
    // `ds_read_b64_tr_b8` requires this input footprint (each lane reads 8 fp8 bytes):
    //   * L00: [0, 00~07], L01: [0, 08~15], L08: [0, 16~23], L09: [0, 24~31]
    //   * L02: [1, 00~07], L03: [1, 08~15], L10: [1, 16~23], L11: [1, 24~31]
    //   * L04: [2, 00~07], L05: [2, 08~15], L12: [2, 16~23], L13: [2, 24~31]
    //   * L06: [3, 00~07], L07: [3, 08~15], L14: [3, 16~23], L15: [3, 24~31]
    // After the hardware transpose, each lane holds 4 rows x 2 cols of V across the
    // 2 destination GPRs (GPR -> cols c, c+16; GPR+1 -> see finalize_load_transposed_v_to_gpr):
    //   L00: rows[0~3] of cols {00, 16}, L01: rows[0~3] of cols {01, 17}, ...,
    //   L15: rows[0~3] of cols {15, 31}.
    // The 4 lane groups together cover the full 16x32 tile (4 rows each).
    //
    // Template params:
    //   kRowOffset : row offset of the tile within the 32-row LDS V block (0 or 16).
    //   kColOffset : col offset of the tile within the 512-col head_dim (multiple of 32, < 512).
    //   GPR        : index of the first of the 2 destination VGPRs.
    // Runtime param:
    //   p_lds_v    : LDS base address of the current V block (KvManager8bitsV3 layout).
    template <uint32_t kRowOffset, uint32_t kColOffset, uint32_t GPR>
    __device__ __forceinline__ void static load_transposed_v_to_gpr(const uintptr_t p_lds_v)
    {
#if defined(__gfx950__)
        static_assert(((kRowOffset % 16) == 0) && (kRowOffset < T::kBlockN),
                      "load_transpose_v_to_gpr(): Unsupported row offset!");
        static_assert(((kColOffset % 32) == 0) && (kColOffset < 512),
                      "load_transpose_v_to_gpr(): Unsupported column offset!");

        // Per-lane (row, col): ds_read_b64_tr_b8 input footprint (see header above).
        //   row = (lane_idx / 16) * 4 + ((lane_idx % 16) / 2) % 4    ? [0, 16)
        //   col = ((lane_idx % 2) + ((lane_idx % 16) / 8) * 2) * 8   ? {0, 8, 16, 24}
        // See get_block_lane_offset() / get_block_fixed_offset() for the address math.
        const uint32_t lane_idx         = opus::lane_id();
        const uint32_t lane_idx_in_grp  = lane_idx % 16;
        const uint32_t row              = (lane_idx / 16) * 4 + (lane_idx_in_grp / 2) % 4;
        const uint32_t col              = ((lane_idx % 2) + (lane_idx_in_grp / 8) * 2) * 8;
        const uintptr_t p_lds_v_lane    = p_lds_v + get_block_lane_offset(row, col);
        constexpr uint32_t kFixedOffset = get_block_fixed_offset<kRowOffset, kColOffset>();

        hkm::ds_read_b64_tr_b8<GPR>(p_lds_v_lane, kFixedOffset);
#else
        static_assert(false,
                      "KVManager8bitsV3::load_transposed_v_to_gpr() is not expected to be called.");
#endif
    }

    // Repack the output of two adjacent load_transposed_v_to_gpr() calls into the layout
    // that mfma_f32_16x16x32_fp8_fp8 expects for its B operand.
    //
    // After load_transposed_v_to_gpr(), each lane's 2 GPRs are laid out row-major across
    // the local 2-row x 2-col mini-tile (in dword units):
    //   GPR_0   = block[r,   c | r,   c+1]   // row r,   2 cols
    //   GPR_0+1 = block[r+1, c | r+1, c+1]   // row r+1, 2 cols  (this is "GPR_1" of the same call)
    // Calling finalize on the (GPR_0, GPR_1) pair from two adjacent loads rearranges them
    // to column-major (each GPR pair holds one N column with its K rows contiguous):
    //   GPR_0 = block[r, c   | r+1, c  ]   // col c,   2 rows
    //   GPR_1 = block[r, c+1 | r+1, c+1]   // col c+1, 2 rows
    // This is achieved by a single intra-lane `v_swap_b32` between GPR_0+1 and GPR_1
    // (no cross-lane traffic).
    //
    // Template params:
    //   GPR_0, GPR_1 : indices of the first VGPR of two 2-register pairs returned by
    //                  load_transposed_v_to_gpr(). The pairs must not overlap.
    template <uint32_t GPR_0, uint32_t GPR_1>
    __device__ __forceinline__ void static finalize_load_transposed_v_to_gpr()
    {
#if defined(__gfx950__)
        asm volatile("v_swap_b32 v[%0], v[%1]" : : "i"(GPR_0 + 1), "i"(GPR_1));
#else
        static_assert(
            false,
            "KVManager8bitsV3::finalize_load_transposed_v_to_gpr() is not expected to be called.");
#endif
    }
};

template <typename T>
class VtManager8bitsV1
{
    private:
    using kv_t = T::kv_t;

    static constexpr uint32_t kNumRowsPerThr    = 4;
    static constexpr uint32_t kNumColsPerThr    = 8;
    static constexpr uint32_t kNumElemsPerBlock = kNumRowsPerThr * kNumColsPerThr; // 4 * 8 = 32
    static constexpr uint32_t kNumBlocksPerRow  = T::kVoHeadDim / kNumColsPerThr;  // 512 / 8 = 64
    static constexpr uint32_t kNumBlocksPerRowWithPadding = kNumBlocksPerRow + 2;  // 64 + 2 = 66

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        constexpr uint32_t kNumSubBlock = 8;
        // 8*((32/8)*512*1+16*4)=8*(4*512+64)=8*2112=16896
        return kNumSubBlock *
               ((T::kBlockN / kNumSubBlock) * T::kVoHeadDim * sizeof(kv_t) + 16 * sizeof(uint32_t));
    }

    // After loading, the elements are in the following layout:
    // [0, 0-7], [1, 0-7], [2, 0-7], [3, 0-7], (done by warp 0 thread 0)
    // [0, 8-15], [1, 8-15], [2, 8-15], [3, 8-15] (done by warp 0 thread 1)
    // ...
    // [0, 120-127], [1, 120-127], [2, 120-127], [3, 120-127] (done by warp 0 thread 15)
    // [0, 128-135], [1, 128-135], [2, 128-135], [3, 128-135] (done by warp 2 thread 0)
    // ...
    // [0, 504-511], [1, 504-511], [2, 504-511], [3, 504-511] (done by warp 6 thread 15)
    // Pad 64 bytes/16 DWORDs for avoiding bank conflicts.
    // [4, 0-7], [5, 0-7], [6, 0-7], [7, 0-7] (done by warp 0 thread 16)
    // ...
    // [4, 504-511], [5, 504-511], [6, 504-511], [7, 504-511] (done by warp 6 thread 31)
    // Pad 64 bytes/16 DWORDs
    // [8, 0-7], [9, 0-7], [10, 0-7], [11, 0-7] (done by warp 0 thread 32)
    // ...
    // [8, 504-511], [9, 504-511], [10, 504-511], [11, 504-511] (done by warp 6 thread 47)
    // Pad 64 bytes/16 DWORDs
    // [12, 0-7], [13, 0-7], [14, 0-7], [15, 0-7] (done by warp 0 thread 48)
    // ...
    // [12, 504-511], [13, 504-511], [14, 504-511], [15, 504-511] (done by warp 6 thread 63)
    // Pad 64 bytes/16 DWORDs
    // [16, 0-7], [17, 0-7], [18, 0-7], [19, 0-7] (done by warp 1 thread 0)
    // ...
    // [16, 504-511], [17, 504-511], [18, 504-511], [19, 504-511] (done by warp 7 thread 15)
    // Pad 64 bytes/16 DWORDs
    // [20, 0-7], [21, 0-7], [22, 0-7], [23, 0-7] (done by warp 1 thread 16)
    // ...
    // [20, 504-511], [21, 504-511], [22, 504-511], [23, 504-511] (done by warp 7 thread 31)
    // Pad 64 bytes/16 DWORDs
    // [24, 0-7], [25, 0-7], [26, 0-7], [27, 0-7] (done by warp 1 thread 32)
    // ...
    // [24, 504-511], [25, 504-511], [26, 504-511], [27, 504-511] (done by warp 7 thread 47)
    // Pad 64 bytes/16 DWORDs
    // [28, 0-7], [29, 0-7], [30, 0-7], [31, 0-7] (done by warp 1 thread 48)
    // ...
    // [28, 504-511], [29, 504-511], [30, 504-511], [31, 504-511] (done by warp 7 thread 63)
    __device__ __forceinline__ static void store_transposed_v_to_lds(const uintptr_t p_lds_vt,
                                                                     const uint32_t warp_idx,
                                                                     const v8ui& v_transposed)
    {
        const uint32_t lane_idx = opus::lane_id();

        // 4x8 block-wise row major layout. No padding between rows or columns.
        const uint32_t row_blk = (warp_idx % 2) * 4 + lane_idx / 16;
        const uint32_t col_blk = (lane_idx % 16) + warp_idx / 2 * 16;
        const uint32_t block_offset =
            (row_blk * kNumBlocksPerRowWithPadding + col_blk) * kNumElemsPerBlock * sizeof(kv_t);
        const uintptr_t p_lds_vt_lane = p_lds_vt + block_offset;

        hkm::ds_write_b128(v_transposed.lo, p_lds_vt_lane, 0);
        hkm::ds_write_b128(v_transposed.hi, p_lds_vt_lane, sizeof(v4ui));
    }

    // load 32x16 block for each warp. Each thread takes 2x4 elements.
    template <uint32_t kRowOffset, uint32_t kColOffset, uint32_t GPR>
    __device__ __forceinline__ void static load_transposed_v_to_gpr(const uintptr_t p_lds_vt)
    {
        constexpr uint32_t kNumDwPerBlock =
            kNumElemsPerBlock / (sizeof(uint32_t) / sizeof(kv_t)); // 32 / 4 = 8
        constexpr uint32_t kOffsetTlBl = 4 * kNumBlocksPerRowWithPadding * kNumElemsPerBlock *
                                         sizeof(kv_t); // 4 * 66 * 32 * 1 = 8448

        constexpr uint32_t kFixedColBlk      = kColOffset / kNumColsPerThr;
        constexpr uint32_t kFixedBlockOffset = kFixedColBlk * kNumElemsPerBlock * sizeof(kv_t);

        static_assert(kRowOffset == 0, "load_transpose_v_to_gpr(): Unsupported row offset!");
        static_assert(((kColOffset % 16) == 0) && (kColOffset < 512),
                      "load_transpose_v_to_gpr(): Unsupported column offset!");

        const uint32_t lane_idx = opus::lane_id();

        // calculate logical coordinate of top-left dw
        const uint32_t row_blk = lane_idx / 16; // 16: 16x16 mfma tile.
        const uint32_t col_blk = (lane_idx % 16) / kNumColsPerThr;
        const uint32_t block_offset =
            (row_blk * kNumBlocksPerRowWithPadding + col_blk) * kNumElemsPerBlock * sizeof(kv_t);

        const uint32_t row_inblk = lane_idx % kNumRowsPerThr;
        const uint32_t col_inblk = ((lane_idx % kNumDwPerBlock) / kNumRowsPerThr) * kNumRowsPerThr;
        const uint32_t inblock_offset = (row_inblk * kNumColsPerThr + col_inblk) * sizeof(kv_t);

        const uintptr_t p_lds_vt_ul_lane = p_lds_vt + block_offset + inblock_offset;

        hkm::ds_read_b32<GPR + 0>(p_lds_vt_ul_lane, kFixedBlockOffset);
        hkm::ds_read_b32<GPR + 1>(p_lds_vt_ul_lane, kFixedBlockOffset + kOffsetTlBl);
    }

    __device__ __forceinline__ static void transpose_v(v8ui* p_v)
    {
        constexpr uint32_t perm_0 = 0x05010400;
        constexpr uint32_t perm_1 = 0x05040100;
        constexpr uint32_t perm_2 = 0x07060302;
        constexpr uint32_t perm_3 = 0x07030602;

        const uint32_t t0_0 = __builtin_amdgcn_perm((*p_v)[2], (*p_v)[0], perm_0);
        const uint32_t t2_0 = __builtin_amdgcn_perm((*p_v)[2], (*p_v)[0], perm_3);
        const uint32_t t0_1 = __builtin_amdgcn_perm((*p_v)[3], (*p_v)[1], perm_0);
        const uint32_t t2_1 = __builtin_amdgcn_perm((*p_v)[3], (*p_v)[1], perm_3);

        const uint32_t t1_0 = __builtin_amdgcn_perm((*p_v)[6], (*p_v)[4], perm_0);
        const uint32_t t3_0 = __builtin_amdgcn_perm((*p_v)[6], (*p_v)[4], perm_3);
        const uint32_t t1_1 = __builtin_amdgcn_perm((*p_v)[7], (*p_v)[5], perm_0);
        const uint32_t t3_1 = __builtin_amdgcn_perm((*p_v)[7], (*p_v)[5], perm_3);

        const uint32_t r0_0 = __builtin_amdgcn_perm(t1_0, t0_0, perm_1);
        const uint32_t r1_0 = __builtin_amdgcn_perm(t1_0, t0_0, perm_2);
        const uint32_t r2_0 = __builtin_amdgcn_perm(t3_0, t2_0, perm_1);
        const uint32_t r3_0 = __builtin_amdgcn_perm(t3_0, t2_0, perm_2);

        const uint32_t r0_1 = __builtin_amdgcn_perm(t1_1, t0_1, perm_1);
        const uint32_t r1_1 = __builtin_amdgcn_perm(t1_1, t0_1, perm_2);
        const uint32_t r2_1 = __builtin_amdgcn_perm(t3_1, t2_1, perm_1);
        const uint32_t r3_1 = __builtin_amdgcn_perm(t3_1, t2_1, perm_2);

        (*p_v)[0] = r0_0;
        (*p_v)[1] = r0_1;
        (*p_v)[2] = r1_0;
        (*p_v)[3] = r1_1;
        (*p_v)[4] = r2_0;
        (*p_v)[5] = r2_1;
        (*p_v)[6] = r3_0;
        (*p_v)[7] = r3_1;
    }
};

// Convert float32 data in pinned GPR to 16-bit data and store to VRAM.
template <typename T, typename out_t>
class OManager16bitsV1
{
    private:
    static_assert(sizeof(out_t) == 2, "Output type must be 16 bits");

    // All come from the result of mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaCols = 16;

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return 0; // Not used
    }

    // Convert one 16x32 MFMA-result tile (8 float32 elements per lane) and store to VRAM.
    // GPR_START: starting GPR index of the 16x32 tile.
    // kColOffset: element-wise column offset in the output buffer.
    // kCheckOOB: when true, set num_records = (qo_end - qo_start) * rowstride
    // bytes so HW suppresses lanes whose lane-offset falls past the per-batch
    // valid range. When false (e.g. for split_output where the host allocates
    // the full extent), use the unbounded SRSRC. qo_end is ignored in the
    // latter case. In both branches the base pointer is advanced to the start
    // of the qo_start row so the lane offset is small and fits the 32-bit
    // V# num_records field even for very large total_q.
    template <uint32_t GPR_START, uint32_t kColOffset, bool kCheckOOB>
    __device__ __forceinline__ void output_to_vram(const out_t* p_output,
                                                   const uint32_t warp_idx,
                                                   const uint32_t qo_start,
                                                   const uint32_t qo_end,
                                                   const uintptr_t p_lds,
                                                   const uint32_t num_qheads)
    {
        static_assert((kColOffset % 32) == 0, "kColOffset must be divisible by 32");

        constexpr uint32_t kOffsetInBytes0 = kColOffset * sizeof(out_t);
        constexpr uint32_t kOffsetInBytes1 = kOffsetInBytes0 + kMfmaCols * sizeof(out_t);

        const uint32_t lane_idx     = opus::lane_id();
        const uint32_t row_idx      = lane_idx % 16 + warp_idx * 16;
        const uint32_t col_idx_base = (lane_idx / 16) * 4;
        const uint32_t offset       = (row_idx * T::kVoHeadDim + col_idx_base) * sizeof(out_t);

        const uintptr_t p_output_batch =
            reinterpret_cast<uintptr_t>(p_output) +
            uint64_t(qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t);
        const uint32_t num_records =
            kCheckOOB ? ((qo_end - qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t))
                      : 0xFFFFFFFFu;
        const hk::buffer_resource out_br = hk::make_buffer_resource(
            static_cast<uint64_t>(p_output_batch), num_records, 0x00020000);

        v2ui b16_pair_0;
        v2ui b16_pair_1;

        if constexpr(std::is_same_v<out_t, hk::bf16>)
        {
            b16_pair_0[0] = float_2_bf16_pair<T::kRoundMode>(GPR_START, GPR_START + 1);
            b16_pair_0[1] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 2, GPR_START + 3);
            b16_pair_1[0] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 4, GPR_START + 5);
            b16_pair_1[1] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 6, GPR_START + 7);
        }
        else
        {
            static_assert(false, "Unsupported output type");
        }

        asm volatile("buffer_store_dwordx2 %0, %2, %3, 0 offen offset:%4\n\t"
                     "buffer_store_dwordx2 %1, %2, %3, 0 offen offset:%5"
                     :
                     : "v"(b16_pair_0),
                       "v"(b16_pair_1),
                       "v"(offset),
                       "s"(*(hk::i32x4*)&out_br),
                       "i"(kOffsetInBytes0),
                       "i"(kOffsetInBytes1)
                     : "memory");
    }
};

// Compared with OManager16bitsV1, this version changes the layout of data in GPR via LDS before
// storing to VRAM so that adjacent lanes write into the same cache line.
template <typename T, typename out_t>
class OManager16bitsV2
{
    private:
    static_assert(sizeof(out_t) == 2, "Output type must be 16 bits");

    static constexpr uint32_t kNumRows                = 16;
    static constexpr uint32_t kNumCols                = 32;
    static constexpr uint32_t kNumPaddingElemPer2Rows = 4;
    static constexpr uint32_t kNumElemPerPadded2Rows  = 2 * kNumCols + kNumPaddingElemPer2Rows;
    static constexpr uint32_t kVramStElemPerLane =
        4 * sizeof(uint32_t) / sizeof(out_t); // use buffer_store_dwordx4
    static constexpr uint32_t kVramStLanePerRow = kNumCols / kVramStElemPerLane; // 32/8=4

    // All come from the result of mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaRows = 16;
    static constexpr uint32_t kMfmaCols = 16;
    static constexpr uint32_t kMfmaElemPerLane =
        kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*16/64=4

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_per_warp_in_byte()
    {
        return (kNumRows / 2) * kNumElemPerPadded2Rows *
               sizeof(out_t); // (16/2)*(32*2+2)*2=8*66*2=1056
    }

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return T::kNumWarps * get_lds_size_per_warp_in_byte(); // 8*1056=8448
    }

    // Convert one 16x32 MFMA-result tile (8 float32 elements per lane) and store to VRAM.
    // GPR_START: starting GPR index of the 16x32 tile.
    // kColOffset: element-wise column offset in the output buffer.
    // See OManager16bitsV1 for the kCheckOOB contract.
    template <uint32_t GPR_START, uint32_t kColOffset, bool kCheckOOB>
    __device__ __forceinline__ void output_to_vram(const out_t* p_output,
                                                   const uint32_t warp_idx,
                                                   const uint32_t qo_start,
                                                   const uint32_t qo_end,
                                                   const uintptr_t p_lds,
                                                   const uint32_t num_qheads)
    {
        static_assert((kColOffset % 32) == 0, "kColOffset must be divisible by 32");

        constexpr uint32_t kOffsetInBytes = kColOffset * sizeof(out_t);

        const uint32_t lane_idx = opus::lane_id();

        const uintptr_t p_lds_warp = p_lds + warp_idx * get_lds_size_per_warp_in_byte();

        auto get_v_offset_lds = [&](const uint32_t row, const uint32_t col) -> uint32_t {
            return ((row / 2) * kNumElemPerPadded2Rows + (row % 2) * kNumCols + col) *
                   sizeof(out_t);
        };

        const uint32_t row_lds_st      = lane_idx % kNumRows;
        const uint32_t col_lds_st      = (lane_idx / kNumRows) * kMfmaElemPerLane;
        const uint32_t v_offset_lds_st = get_v_offset_lds(row_lds_st, col_lds_st);

        const uint32_t row_lds_ld      = lane_idx / kVramStLanePerRow;
        const uint32_t col_lds_ld      = (lane_idx % kVramStLanePerRow) * kVramStElemPerLane;
        const uint32_t v_offset_lds_ld = get_v_offset_lds(row_lds_ld, col_lds_ld);

        const uint32_t row_vram_st = row_lds_ld + warp_idx * kNumRows;
        const uint32_t col_vram_st = col_lds_ld;
        const uint32_t v_offset_vram_st =
            (row_vram_st * T::kVoHeadDim + col_vram_st) * sizeof(out_t);

        const uintptr_t p_output_batch =
            reinterpret_cast<uintptr_t>(p_output) +
            uint64_t(qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t);
        const uint32_t num_records =
            kCheckOOB ? ((qo_end - qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t))
                      : 0xFFFFFFFFu;
        const hk::buffer_resource out_br = hk::make_buffer_resource(
            static_cast<uint64_t>(p_output_batch), num_records, 0x00020000);

        v2ui b16_pair_0;
        v2ui b16_pair_1;

        if constexpr(std::is_same_v<out_t, hk::bf16>)
        {
            b16_pair_0[0] = float_2_bf16_pair<T::kRoundMode>(GPR_START, GPR_START + 1);
            b16_pair_0[1] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 2, GPR_START + 3);
            b16_pair_1[0] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 4, GPR_START + 5);
            b16_pair_1[1] = float_2_bf16_pair<T::kRoundMode>(GPR_START + 6, GPR_START + 7);
        }
        else
        {
            static_assert(false, "Unsupported output type");
        }

        hkm::ds_write_b64(b16_pair_0, p_lds_warp + v_offset_lds_st, 0);
        hkm::ds_write_b64(b16_pair_1, p_lds_warp + v_offset_lds_st, kNumCols / 2 * sizeof(out_t));
        asm volatile("s_waitcnt lgkmcnt(0)");
        const v4ui data = hkm::ds_read_b128(p_lds_warp + v_offset_lds_ld, 0);
        asm volatile("s_waitcnt lgkmcnt(0)");
        hkm::buffer_store_dwordx4(data, out_br, v_offset_vram_st, 0, kOffsetInBytes);
    }
};

// Store float32 data from pinned GPR to VRAM (no conversion; out_t must be float).
template <typename T, typename out_t>
class OManager32bitsV1
{
    private:
    static_assert(sizeof(out_t) == 4, "Output type must be 32 bits");

    // All come from the result of mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaCols = 16;

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return 0; // Not used
    }

    // Convert one 16x32 MFMA-result tile (8 float32 elements per lane) and store to VRAM.
    // GPR_START: starting GPR index of the 16x32 tile.
    // kColOffset: element-wise column offset in the output buffer.
    // See OManager16bitsV1 for the kCheckOOB contract.
    template <uint32_t GPR_START, uint32_t kColOffset, bool kCheckOOB>
    __device__ __forceinline__ void output_to_vram(const out_t* p_output,
                                                   const uint32_t warp_idx,
                                                   const uint32_t qo_start,
                                                   const uint32_t qo_end,
                                                   const uintptr_t p_lds,
                                                   const uint32_t num_qheads)
    {
        static_assert((kColOffset % 32) == 0, "kColOffset must be divisible by 32");

        constexpr uint32_t kOffsetInBytes0 = kColOffset * sizeof(out_t);
        constexpr uint32_t kOffsetInBytes1 = kOffsetInBytes0 + kMfmaCols * sizeof(out_t);

        const uint32_t lane_idx     = opus::lane_id();
        const uint32_t row_idx      = lane_idx % 16 + warp_idx * 16;
        const uint32_t col_idx_base = (lane_idx / 16) * 4;
        const uint32_t offset       = (row_idx * T::kVoHeadDim + col_idx_base) * sizeof(out_t);

        const uintptr_t p_output_batch =
            reinterpret_cast<uintptr_t>(p_output) +
            uint64_t(qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t);
        const uint32_t num_records =
            kCheckOOB ? ((qo_end - qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t))
                      : 0xFFFFFFFFu;
        const hk::buffer_resource out_br = hk::make_buffer_resource(
            static_cast<uint64_t>(p_output_batch), num_records, 0x00020000);

        if constexpr(std::is_same_v<out_t, float>)
        {
            hkm::buffer_store_dwordx4<GPR_START>(out_br, offset, 0, kOffsetInBytes0);
            hkm::buffer_store_dwordx4<GPR_START + 4>(out_br, offset, 0, kOffsetInBytes1);
        }
        else
        {
            static_assert(false, "Unsupported output type");
        }
    }
};

// Compared with OManager32bitsV1, this version changes the layout of data in GPR via LDS before
// storing to VRAM so that adjacent lanes write into the same cache line.
template <typename T, typename out_t>
class OManager32bitsV2
{
    private:
    static_assert(sizeof(out_t) == 4, "Output type must be 32 bits");

    static constexpr uint32_t kNumRows              = 16;
    static constexpr uint32_t kNumCols              = 32;
    static constexpr uint32_t kNumPaddingElemPerRow = 4;
    static constexpr uint32_t kNumElemPerPaddedRow  = kNumCols + kNumPaddingElemPerRow; // 32+4=36
    static constexpr uint32_t kVramStElemPerLane =
        4 * sizeof(uint32_t) / sizeof(out_t); // use buffer_store_dwordx4
    static constexpr uint32_t kVramStLanePerRow = kNumCols / kVramStElemPerLane; // 32/4=8
    static constexpr uint32_t kVramStRowsPerRnd =
        opus::get_warp_size() / kVramStLanePerRow; // 64/8=8
    static constexpr uint32_t kLdsLdOffsetDeltaInBytes =
        kVramStRowsPerRnd * kNumElemPerPaddedRow * sizeof(out_t); // 8*36*4=1152
    static constexpr uint32_t kVramStOffsetDeltaInBytes =
        kVramStRowsPerRnd * T::kVoHeadDim * sizeof(out_t); // 8*512*4=16384

    // All come from the result of mfma_f32_16x16x32_fp8_fp8.
    static constexpr uint32_t kMfmaRows = 16;
    static constexpr uint32_t kMfmaCols = 16;
    static constexpr uint32_t kMfmaElemPerLane =
        kMfmaRows * kMfmaCols / opus::get_warp_size(); // 16*16/64=4

    __device__ __forceinline__ static constexpr uint32_t get_lds_size_per_warp_in_byte()
    {
        return kNumRows * kNumElemPerPaddedRow * sizeof(out_t); // 16*36*4=2304
    }

    public:
    __device__ __forceinline__ static constexpr uint32_t get_lds_size_in_byte()
    {
        return T::kNumWarps * get_lds_size_per_warp_in_byte(); // 8*2304=18432
    }

    // Convert one 16x32 MFMA-result tile (8 float32 elements per lane) and store to VRAM.
    // GPR_START: starting GPR index of the 16x32 tile.
    // kColOffset: element-wise column offset in the output buffer.
    // See OManager16bitsV1 for the kCheckOOB contract.
    template <uint32_t GPR_START, uint32_t kColOffset, bool kCheckOOB>
    __device__ __forceinline__ void output_to_vram(const out_t* p_output,
                                                   const uint32_t warp_idx,
                                                   const uint32_t qo_start,
                                                   const uint32_t qo_end,
                                                   const uintptr_t p_lds,
                                                   const uint32_t num_qheads)
    {
        static_assert((kColOffset % 32) == 0, "kColOffset must be divisible by 32");
        constexpr uint32_t kOffsetInBytes = kColOffset * sizeof(out_t);

        const uint32_t lane_idx = opus::lane_id();

        const uintptr_t p_lds_warp = p_lds + warp_idx * get_lds_size_per_warp_in_byte();

        auto get_v_offset_lds = [&](const uint32_t row, const uint32_t col) -> uint32_t {
            return (row * kNumElemPerPaddedRow + col) * sizeof(out_t);
        };

        const uint32_t row_lds_st      = lane_idx % kNumRows;
        const uint32_t col_lds_st      = (lane_idx / kNumRows) * kMfmaElemPerLane;
        const uint32_t v_offset_lds_st = get_v_offset_lds(row_lds_st, col_lds_st);

        const uint32_t row_lds_ld      = lane_idx / kVramStLanePerRow;
        const uint32_t col_lds_ld      = (lane_idx % kVramStLanePerRow) * kVramStElemPerLane;
        const uint32_t v_offset_lds_ld = get_v_offset_lds(row_lds_ld, col_lds_ld);

        const uint32_t row_vram_st = row_lds_ld + warp_idx * kNumRows;
        const uint32_t col_vram_st = col_lds_ld;
        const uint32_t v_offset_vram_st =
            (row_vram_st * T::kVoHeadDim + col_vram_st) * sizeof(out_t);

        const uintptr_t p_output_batch =
            reinterpret_cast<uintptr_t>(p_output) +
            uint64_t(qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t);
        const uint32_t num_records =
            kCheckOOB ? ((qo_end - qo_start) * num_qheads * T::kVoHeadDim * sizeof(out_t))
                      : 0xFFFFFFFFu;
        const hk::buffer_resource out_br = hk::make_buffer_resource(
            static_cast<uint64_t>(p_output_batch), num_records, 0x00020000);

        if constexpr(std::is_same_v<out_t, float>)
        {
            // This waitcnt is not necessary but good for performance for unknown reason.
            asm volatile("s_waitcnt vmcnt(0)");
            hkm::ds_write_b128<GPR_START>(p_lds_warp + v_offset_lds_st, 0);
            hkm::ds_write_b128<GPR_START + 4>(p_lds_warp + v_offset_lds_st,
                                              kNumCols / 2 * sizeof(out_t));
        }
        else
        {
            static_assert(false, "Unsupported output type");
        }

        asm volatile("s_waitcnt lgkmcnt(0)");
        const v4ui data_0 = hkm::ds_read_b128(p_lds_warp + v_offset_lds_ld, 0);
        const v4ui data_1 =
            hkm::ds_read_b128(p_lds_warp + v_offset_lds_ld, kLdsLdOffsetDeltaInBytes);
        asm volatile("s_waitcnt lgkmcnt(0)");
        hkm::buffer_store_dwordx4(data_0, out_br, v_offset_vram_st, 0, kOffsetInBytes);
        hkm::buffer_store_dwordx4(
            data_1, out_br, v_offset_vram_st + kVramStOffsetDeltaInBytes, 0, kOffsetInBytes);
    }
};
